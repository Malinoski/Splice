archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.respiration.v1

concept
	[at0000]	-- Respirations
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			author = <
				["name"] = <"Carlos Porto Filho">
				["organisation"] = <"USP">
			>
		>
	>
description
	original_author = <
		["name"] = <"Ian McNicoll">
		["organisation"] = <"Ocean Informatics, United Kingdom">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"2009-07-17">
	>
	details = <
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Registrar as características da respiração espontânea.">
			use = <"Usar para registrar as características observadas e medidas relacionadas com as respirações espontâneas em uma pessoa, incluindo frequência respiratória, profundidade e ritmo.

As respirações são comumente registradas como um componente de sinais vitais - incluindo pressão arterial, pulso, temperatura e oximetria. Há arquétipo específicos adicionais para cada um desses conceitos.

As respirações devem ser medidas antes de refeições em recém-nascidos e crianças jovens.">
			keywords = <"*respirações(pt-BR)", "*respirando(pt-BR)", "*respiração(pt-BR)", "*resps(en)", "*respiração(pt-BR)">
			misuse = <"Não usar para tentar registrar outros aspectos de exame respiratório em geral. Outros arquétipos específicos serão utilizados para registrar características tais como esforço respiratório, achados auscultatórios, etc.

Não usar para registrar detalhes quando o paciente está sob ventilação assistida.">
			copyright = <"© openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the observed characteristics of spontaneous breathing.">
			use = <"Use to record the observed and measured characteristics related to spontaneous respirations in a person, including respiratory rate, depth and rhythm.

Respirations are commonly recorded as one component of Vital signs - comprising Blood Pressure, Pulse, Temperature, and Oximetry.  There are additional specific archetypes for each of these concepts.

Respirations should be measured before feeds in neonates and young infants.">
			keywords = <"respirations", "breathing", "breath", "resps", "respiration">
			misuse = <"Not to be used to try to record other aspects of the broader respiratory examination or assessment.  Other specific archetypes will be used to record characteristics such as respiratory effort, auscultatory findings etc.

Not to be used to record details  when the subject is undergoing assisted ventilation.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Marja Buur, Medisch Centrum Alkmaar/ Code24, Netherlands", "Gregory Caulton, PatientOS Inc., United States", "Stephen Chu, NeHTA, Australia", "Sebastian Garde, Ocean Informatics, Germany", "Anne Harbison, CPCER, Australia", "Sam Heard, Ocean Informatics, Australia", "Omer Hotomaroglu, Turkey", "Sundaresan Jagannathan, Scottish NHS, United Kingdom", "Andrew James, University of Toronto, Canada", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Rikard Lovstrom, Swedish Medical Association, Sweden", "Ian McNicoll, Ocean Informatics, United Kingdom (Editor)", "Jeroen Meintjens, Medisch Centrum Alkmaar, Netherlands", "Soon Ghee Yap, Singapore Health Services Pte Ltd, Singapore">
	other_details = <
		["references"] = <"Braun RB.  Respiratory Rate and Pattern. In: Walker HK, Hall WD, Hurst JW. Clinical methods: the history, physical, and laboratory examinations. [Internet] 3rd ed. Stoneham (MA): Butterworth Publishers; c1990 [cited 2009 Jul 21] 
Available from: http://www.ncbi.nlm.nih.gov/bookshelf/br.fcgi?book=cm&part=A1308 [Accessed July 22, 2009].

Gaunt AC, Frang T. Examination of the Respiratory System. The Journal of Clinical Examination 2007 (4): 14-22 
Available from:   http://www.thejce.com/downloads/journals/oct_2007/14-22%20Gaunt%20and%20Frang%20-%20Respiratory%20System.pdf  [Accessed July 22, 2009]

Cheyne-Stokes respiration - Wikipedia, the free encyclopedia [Internet]. [cited 2009 Jul 25 ] 
Available from: http://en.wikipedia.org/wiki/Cheyne-Stokes_respiration

Biot's respiration - Wikipedia, the free encyclopedia [Internet]. [cited 2009 Jul 25 ] 
Available from: http://en.wikipedia.org/wiki/Biot%27s_respiration

Apneustic respirations - Wikipedia, the free encyclopedia [Internet]. [cited 2009 Jul 25 ] 
Available from: http://en.wikipedia.org/wiki/Apneustic_respirations">
		["MD5-CAM-1.0.1"] = <"F785361CE72E20207CD557387B49B703">
	>

definition
	OBSERVATION[at0000] matches {	-- Respirations
		data matches {
			HISTORY[at0001] matches {	-- history
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- List
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {1..1} matches {	-- Rate
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::382]>
												list = <
													["1"] = <
														units = <"/min">
														magnitude = <|0.0..200.0|>
														precision = <|0|>
													>
												>
											>
										}
									}
									ELEMENT[at0005] occurrences matches {1..1} matches {	-- Rhythm
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0006, 	-- Regular
													at0007]	-- Irregular
												}
											}
										}
									}
									ELEMENT[at0016] occurrences matches {0..1} matches {	-- Depth
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0017, 	-- Normal
													at0018, 	-- Shallow
													at0019, 	-- Deep
													at0025]	-- Variable
												}
											}
										}
									}
									ELEMENT[at0024] occurrences matches {0..1} matches {	-- Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {	-- Abnormal respiratory pattern
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0012, 	-- Kussmaul's respiration
													at0030, 	-- Cheyne-Stokes respiration
													at0031, 	-- Ataxic respiration
													at0033, 	-- Apneustic respiration
													at0034, 	-- Cluster breathing
													at0044, 	-- Apnoea
													at0054]	-- Prolonged expiratory phase
												}
											}
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0022] matches {	-- List
								items cardinality matches {0..*; unordered} matches {
									allow_archetype CLUSTER[at0055] occurrences matches {0..1} matches {	-- Ambient oxygen
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.ambient_oxygen(-[a-zA-Z0-9_]+)*\.v1/}
									}
									ELEMENT[at0056] occurrences matches {0..*} matches {	-- Confounding factors
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at0037] occurrences matches {0..1} matches {	-- Exertion
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.level_of_exertion(-[a-zA-Z0-9_]+)*\.v1/}
									}
								}
							}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["pt-br"] = <
			items = <
				["at0000"] = <
					text = <"Respirações">
					description = <"As características observadas da respiração espontânea como seria comumente registrada como parte de uma observação de um \"sinal vital\".">
				>
				["at0001"] = <
					text = <"*history(en)">
					description = <"*@ internal @(en)">
				>
				["at0002"] = <
					text = <"Qualquer evento">
					description = <"Evento genérico">
				>
				["at0003"] = <
					text = <"*List(en)">
					description = <"*@ internal @(en)">
				>
				["at0004"] = <
					text = <"Frequência">
					description = <"Frequência respiratória">
				>
				["at0005"] = <
					text = <"Ritmo">
					description = <"Ritmo respiratório">
				>
				["at0006"] = <
					text = <"Regular">
					description = <"Respiração regular">
				>
				["at0007"] = <
					text = <"Irregular">
					description = <"Respiração irregular">
				>
				["at0009"] = <
					text = <"Padrão respiratório anormal">
					description = <"Padrão específico de respiração anormal. Outros padrões podem ser colocados como texto livre ou subconjunto terminológico definido no nível da especialização ou no nível do template.">
				>
				["at0012"] = <
					text = <"Respiração de Kussmaul">
					description = <"Respiração profunda com ou sem suspiro.">
				>
				["at0016"] = <
					text = <"Profundidade">
					description = <"Pronfundidade respiratória">
				>
				["at0017"] = <
					text = <"Normal">
					description = <"Profundidade respiratória normal">
				>
				["at0018"] = <
					text = <"Superficial">
					description = <"Profundidade respiratória superficial">
				>
				["at0019"] = <
					text = <"Profunda">
					description = <"Respiração profunda">
				>
				["at0022"] = <
					text = <"*List(en)">
					description = <"*@ internal @(en)">
				>
				["at0024"] = <
					text = <"Descrição">
					description = <"A descrição textual das respirações">
				>
				["at0025"] = <
					text = <"Variável">
					description = <"Profundidade respiratória variável">
				>
				["at0030"] = <
					text = <"Respiração de Cheyne-Stokes">
					description = <"Períodos de hiperventilação alternados com períodos de apneia.">
				>
				["at0031"] = <
					text = <"Respiração atáxica">
					description = <"Respiração de frequência e volume corrente variáveis">
				>
				["at0033"] = <
					text = <"Respiração apneustica">
					description = <"Profunda, inspiração com suspiro com uma pausa na inspiração total seguida de uma breve insuficiência na expiração.">
				>
				["at0034"] = <
					text = <"Respiração de Biot">
					description = <"Grupos de respirações profundas irregulares que se alternam com períodos de apneia.">
				>
				["at0037"] = <
					text = <"Esforço">
					description = <"Nível de esforço durante ou logo antes de ser feita a observação. Destinada apenas para registrar o esforço somente se isso pode afetar as respirações e onde normalmente não seria registrado com parte de uma observação clínica geral.">
				>
				["at0044"] = <
					text = <"Apneia.">
					description = <"Parada da respiração.">
				>
				["at0054"] = <
					text = <"Expiração prolongada">
					description = <"A fase da expiração é mais longa que o normal/usual. Associada com doenças obstrutivas das vias respiratórias tais como asma.">
				>
				["at0055"] = <
					text = <"Oxigênio ambiente">
					description = <"Detalhes da quantidade de oxigênio sendo administrada ao paciente no momento da observação. Se assumem valores de concentração de oxigênio de 21%, Fi02 de 0,21 e velocidade de fluxo do oxigênio de 0 l/min ou 0 ml/min.">
				>
				["at0056"] = <
					text = <"Fatores de confundimento">
					description = <"Comenta e registra outros fatores incidentais que podem contribuir para as respirações observadas. Por exemplo, nível de ansiedade, dor, alimentação em crianças, traqueostomia, etc.">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Respirations">
					description = <"The observed characteristics of spontaneous breathing as would commonly be recorded as part of a 'vital signs' examination.">
				>
				["at0001"] = <
					text = <"history">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Generic event.">
				>
				["at0003"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Rate">
					description = <"Rate of respiration.">
				>
				["at0005"] = <
					text = <"Rhythm">
					description = <"Rhythm of respiration.">
				>
				["at0006"] = <
					text = <"Regular">
					description = <"Regular respiration.">
				>
				["at0007"] = <
					text = <"Irregular">
					description = <"Irregular respirations.">
				>
				["at0009"] = <
					text = <"Abnormal respiratory pattern">
					description = <"Specific abnormal respiratory pattern. Other patterns may be entered as free text or terminology subset defined at specialisation or template level.">
				>
				["at0012"] = <
					text = <"Kussmaul's respiration">
					description = <"Deep chest breathing with or without a visible gasp.">
				>
				["at0016"] = <
					text = <"Depth">
					description = <"Depth of respiration.">
				>
				["at0017"] = <
					text = <"Normal">
					description = <"Normal depth of breathing.">
				>
				["at0018"] = <
					text = <"Shallow">
					description = <"Shallow depth of breathing.">
				>
				["at0019"] = <
					text = <"Deep">
					description = <"Deep breathing.">
				>
				["at0022"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0024"] = <
					text = <"Description">
					description = <"A text description of respirations.">
				>
				["at0025"] = <
					text = <"Variable">
					description = <"Variable depth of breathing.">
				>
				["at0030"] = <
					text = <"Cheyne-Stokes respiration">
					description = <"Periods of hyperventilation alternating with periods of apnoea.">
				>
				["at0031"] = <
					text = <"Ataxic respiration">
					description = <"Breathing of varying tidal volumes and rates.">
				>
				["at0033"] = <
					text = <"Apneustic respiration">
					description = <"Deep, gasping inspiration with a pause at full inspiration followed by a brief, insufficient release of breath.">
				>
				["at0034"] = <
					text = <"Cluster breathing">
					description = <"Clusters of irregular breaths that alternate with periods of apnoea. Also termed Biot's breathing.">
				>
				["at0037"] = <
					text = <"Exertion">
					description = <"Subject's level of exertion at or just prior to the observation being made. Intended only to record exertion only as it might effect respirations and where it would not normally be recorded as part of general clinical observation.">
				>
				["at0044"] = <
					text = <"Apnoea">
					description = <"Breathing has ceased.">
				>
				["at0054"] = <
					text = <"Prolonged expiratory phase">
					description = <"The respiratory expiratory phase is longer than normal/usual. Associated with obstructive airways disease such as asthma.">
				>
				["at0055"] = <
					text = <"Ambient oxygen">
					description = <"Details of the amount of oxygen being delivered to the subject at the time of observation.  Assumed values of 21% oxygen concentration, Fi02 of 0.21 and oxygen flow rate of 0 l/min or 0 ml/min.">
				>
				["at0056"] = <
					text = <"Confounding factors">
					description = <"Comment on and record other incidental factors that may be contributing to the respirations observation. For example, level of anxiety, pain, feeding in infants, tracheostomy.">
				>
			>
		>
	>
