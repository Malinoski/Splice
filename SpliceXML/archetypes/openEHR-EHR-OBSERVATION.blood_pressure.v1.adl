archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.blood_pressure.v1

concept
	[at0000]	-- Blood Pressure
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			author = <
				["name"] = <"Jussara Rözsch">
				["organisation"] = <"OpenEHR  Foundation">
				["email"] = <"jussara.macedo@gmail.com">
			>
			accreditation = <"Medical Doctor, Psychiarist, Clinical Modeller, openEHR Diretor, ehealth infostructuture WG ccoordinator- brazilian  ehealth program">
		>
	>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"2006-03-22">
	>
	details = <
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Para registrar a pressão arterial sistêmica de um indivíduo.">
			use = <"Usado para registrar todas as representações da medida da  pressão areterial sistêmica, não importando qual método ou localização corporal usada para registrá-la. O objetivo do arquétipo é capturar a pressão sanguínea em todos os cenários clínicos - por exemplo, auto-medida com um aparelho de pressão caseiro; um avaliação de emergência da pressão sistólica usando palpação e esfigmomanômetro; medidas realizadas em consultas clínicas ou durante testes de esforço; e uma série de medidas feitas por uma máquina em uma Unidade de Terapia Intensiva.
Existe um modelo rico que apoia a interpretação de medidas através da identificação da posição do paciente, nível exercício, gatores confundidores e ângulo de uma mesa de inclinação em uma pesquisa.
Eventos nomeados têm sido limitados em médica a um período de 24 horas, entretanto templates podem, posteriormente, restringir o padrão predeterminado 'qualquer evento'  para atender a requisitos específicos registro de medida de pressão sanguínea em pontos no tempo específicos, ou em faixas de intervalos (+/-funções matemáticas).">
			keywords = <"observações", "medidas", "PA", "pressão sanguínea", "sinais vitais", "Pressão arterial média", "Pressão de Pulso", "sistólica", "diástólica">
			misuse = <"Não deve ser usada para registrar pressão intravenosa.
Não deve ser usada para medida da pressão arterial que NÃO é um substituto da pressão arterial na circulação sistêmica, por exemplo, medida específica da pressão da artéria Pulmonar direita.
Em ambas situações use OBSERVATION.intravascular_pressure e especializações relacionadas.">
			copyright = <"© openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the systemic arterial blood pressure of an individual.">
			use = <"Use to record all representations of systemic arterial blood pressure measurement, no matter which method or body location is used to record it. The archetype is intended to capture blood pressure measurements in all clinical scenarios - for example, self-measurement with a home blood pressure machine; an emergency assessment of systolic using palpation and a sphygmomanometer; measurements taken in clinical consultations or during exercise stress testing; and a series of measurements made by a machine in Intensive Care.   
There is a rich state model that supports interpretation of measurements through identifying patient position, exercise, confounding factors and angle of a tilt table in research. 
Named events have been limited to average over a 24 hour period, however templates can further constrain the default 'any event' to cater for specific requirements for blood pressure measurements such as recording Blood Pressure against specific points in time, or over a range of intervals (+/- mathematical functions).">
			keywords = <"observations", "measurement", "bp", "vital signs", "mean arterial pressure", "pulse pressure", "systolic", "diastolic", "RR", "NIBP">
			misuse = <"Not to be used for intravenous pressure.
Not to be used for the measurement of arterial blood pressure which is NOT a surrogate for arterial pressure in the systemic circulation eg specific measurement of right Pulmonary artery pressure.  
Use OBSERVATION.intravascular_pressure and related specialisations in both of these situations.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Koray Atalag, University of Auckland, New Zealand", "Knut Bernstein, MEDIQ, Denmark", "Marja Buur, Medisch Centrum Alkmaar, Netherlands", "Rong Chen, Cambio Healthcare Systems, Sweden", "Beatriz de Faria Leão, Zilics, Brazil", "Paul Donaldson, Nursing Informatics Australia, Australia", "Jose Florez Arango, Universidad de Antioquia, Colombia", "Gerard Freriks, ERC, Netherlands", "Sebastian Garde, Ocean Informatics, Germany", "Anneke Goossen, Results 4 Care, Netherlands", "Sam Heard, Ocean Informatics, Australia", "Karsten Heusser, Hannover Medical School, Germany", "Omer Hotomaroglu, Turkey", "Evelyn Hovenga, EJSH Consulting, Australia", "Derek Hoy, United Kingdom", "Pieter Hummel, Medisch Centrum Alkmaar, Netherlands", "Eugene Igras, IRIS Systems, Inc., Canada", "Sundaresan Jagannathan, Scottish NHS, United Kingdom", "Andrew James, University of Toronto, Canada", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Rikard Lovstrom, Swedish Medical Association, Sweden", "Rohan Martin, Ambulance Victoria, Australia", "Ian McNicoll, Ocean Informatics, United Kingdom", "Jeroen Meintjens, Medisch Centrum Alkmaar, Netherlands", "Udo Müller-Oest, CompuGROUP Software, Germany", "Melvin Reynolds, United Kingdom", "Tony Shannon, NHS, United Kingdom", "Hwei-Yee Tai, Tan Tock Seng Hospital, Singapore", "Stef Verlinden, Vivici, Netherlands", "Soon Ghee Yap, Singapore Health Services Pte Ltd, Singapore">
	other_details = <
		["references"] = <"O'Brien E, Asmar R, Beilin L, et al. European Society of Hypertension recommendations for conventional, ambulatory and home blood pressure measurement. Journal of Hypertension [Internet]. 2003 [cited 2009 Jul 30] ; 21(5):821-848. Available from http://www.bhsoc.org/bp_monitors/ESH_BP_rec.pdf

Perloff D, Grim C, Flack J, Frohlich ED, Hill M, McDonald M, Morgenstern BZ. Human blood pressure determination by sphygmomanometry. Circulation [Internet]. 1993 [cited 2009 Jul 29] 88 (5): 2460.  Available from: http://circ.ahajournals.org/cgi/reprint/88/5/2460">
		["MD5-CAM-1.0.1"] = <"BE0B7519BA42267F15F7C82B9EA42E2E">
	>

definition
	OBSERVATION[at0000] matches {	-- Blood Pressure
		data matches {
			HISTORY[at0001] matches {	-- history
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0006] occurrences matches {0..*} matches {	-- any event
						data matches {
							ITEM_TREE[at0003] matches {	-- blood pressure
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Systolic
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::125]>
												list = <
													["1"] = <
														units = <"mm[Hg]">
														magnitude = <|0.0..<1000.0|>
														precision = <|0|>
													>
												>
											>
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Diastolic
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::125]>
												list = <
													["1"] = <
														units = <"mm[Hg]">
														magnitude = <|0.0..<1000.0|>
														precision = <|0|>
													>
												>
											>
										}
									}
									ELEMENT[at1006] occurrences matches {0..1} matches {	-- Mean Arterial Pressure
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::125]>
												list = <
													["1"] = <
														units = <"mm[Hg]">
														magnitude = <|0.0..<1000.0|>
														precision = <|0|>
													>
												>
											>
										}
									}
									ELEMENT[at1007] occurrences matches {0..1} matches {	-- Pulse Pressure
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::125]>
												list = <
													["1"] = <
														units = <"mm[Hg]">
														magnitude = <|0.0..<1000.0|>
														precision = <|0|>
													>
												>
											>
										}
									}
									ELEMENT[at0033] occurrences matches {0..1} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0007] matches {	-- state structure
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0008] occurrences matches {0..1} matches {	-- Position
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at1000, 	-- Standing
													at1001, 	-- Sitting
													at1002, 	-- Reclining
													at1003, 	-- Lying
													at1014; 	-- Lying with tilt to left
													at1001]	-- assumed value
												}
											}
										}
									}
									ELEMENT[at1052] occurrences matches {0..1} matches {	-- Confounding factors
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at1030] occurrences matches {0..1} matches {	-- Exertion  
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.level_of_exertion(-[a-zA-Z0-9_]+)*\.v1/}
									}
									ELEMENT[at1043] occurrences matches {0..1} matches {	-- Sleep status
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at1044, 	-- Alert & awake
													at1045; 	-- Sleeping
													at1044]	-- assumed value
												}
											}
										}
									}
									ELEMENT[at1005] occurrences matches {0..1} matches {	-- Tilt
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::497]>
												list = <
													["1"] = <
														units = <"°">
														magnitude = <|-90.0..90.0|>
														precision = <|0|>
													>
												>
												assumed_value = <
													magnitude = <0.0>
													units = <"°">
													precision = <0>
												>
											>
										}
									}
								}
							}
						}
					}
					INTERVAL_EVENT[at1042] occurrences matches {0..1} matches {	-- 24 hour average 
						math_function matches {
							DV_CODED_TEXT matches {
								defining_code matches {[openehr::146]}
							}
						}
						width matches {
							DV_DURATION matches {
								value matches {|PT24H|}
							}
						}
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0006]/data[at0003]	-- /data[history]/events[any event]/data[blood pressure]
						}
						state matches {
							use_node ITEM_TREE /data[at0001]/events[at0006]/state[at0007]	-- /data[history]/events[any event]/state[state structure]
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0011] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0013] occurrences matches {0..1} matches {	-- Cuff size
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0015, 	-- Adult Thigh
									at0016, 	-- Large Adult
									at0017, 	-- Adult
									at1008, 	-- Small Adult
									at1009, 	-- Paediatric/Child
									at1018, 	-- Infant
									at1019]	-- Neonatal
								}
							}
						}
					}
					CLUSTER[at1033] occurrences matches {0..1} matches {	-- Location
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0014] occurrences matches {0..1} matches {	-- Location of measurement
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0025, 	-- Right arm
											at0026, 	-- Left arm
											at0027, 	-- Right thigh
											at0028, 	-- Left thigh
											at1020, 	-- Right wrist
											at1021, 	-- Left wrist
											at1026, 	-- Right ankle
											at1031, 	-- Left ankle
											at1032, 	-- Finger
											at1051, 	-- Toe
											at1053]	-- Intra-arterial
										}
									}
								}
							}
							ELEMENT[at1034] occurrences matches {0..1} matches {	-- Specific location
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
					ELEMENT[at1035] occurrences matches {0..1} matches {	-- Method
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at1036, 	-- Auscultation
									at1037, 	-- Palpation
									at1039, 	-- Machine
									at1040]	-- Invasive
								}
							}
						}
					}
					ELEMENT[at1038] occurrences matches {0..1} matches {	-- Mean Arterial Pressure Formula
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at1010] occurrences matches {0..1} matches {	-- Diastolic endpoint
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at1011, 	-- Phase IV
									at1012]	-- Phase V
								}
							}
						}
					}
					allow_archetype CLUSTER[at1025] occurrences matches {0..1} matches {	-- Device
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
				}
			}
		}
	}


ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Blood Pressure">
					description = <"The local measurement of arterial blood pressure which is a surrogate for arterial. pressure in the systemic circulation.  Most commonly, use of the term 'blood pressure' refers to measurement of brachial artery pressure in the upper arm.">
				>
				["at0001"] = <
					text = <"history">
					description = <"History Structural node.">
				>
				["at0003"] = <
					text = <"blood pressure">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Systolic">
					description = <"Peak systemic arterial blood pressure  - measured in systolic or contraction phase of the heart cycle.">
				>
				["at0005"] = <
					text = <"Diastolic">
					description = <"Minimum systemic arterial blood pressure - measured in the diastolic or relaxation phase of the heart cycle.">
				>
				["at0006"] = <
					text = <"any event">
					description = <"Default event.">
				>
				["at0007"] = <
					text = <"state structure">
					description = <"@ internal @">
				>
				["at0008"] = <
					text = <"Position">
					description = <"The position of the subject at the time of measurement.">
				>
				["at0011"] = <
					text = <"Tree">
					description = <"List structure.">
				>
				["at0013"] = <
					text = <"Cuff size">
					description = <"The size of the cuff used for blood pressure measurement.">
					comment = <"Perloff D, Grim C, Flack J, Frohlich ED, Hill M, McDonald M, Morgenstern BZ. Human blood pressure determination by sphygmomanometry. Circulation 1993;88;2460-2470. ">
				>
				["at0014"] = <
					text = <"Location of measurement">
					description = <"Common body sites where blood pressure is recorded.">
				>
				["at0015"] = <
					text = <"Adult Thigh">
					description = <"A cuff used for an adult thigh - bladder approx 20cm x 42cm.">
				>
				["at0016"] = <
					text = <"Large Adult">
					description = <"A cuff for adults with larger arms - bladder approx 16cm x 38cm.">
				>
				["at0017"] = <
					text = <"Adult">
					description = <"A cuff that is standard for an adult - bladder approx 13cm x 30cm.">
				>
				["at0025"] = <
					text = <"Right arm">
					description = <"The right arm of the person.">
				>
				["at0026"] = <
					text = <"Left arm">
					description = <"The left arm of the person.">
				>
				["at0027"] = <
					text = <"Right thigh">
					description = <"The right thigh of the person.">
				>
				["at0028"] = <
					text = <"Left thigh">
					description = <"The left thigh of the person.">
				>
				["at0033"] = <
					text = <"Comment">
					description = <"Comment on blood pressure measurement.">
				>
				["at1000"] = <
					text = <"Standing">
					description = <"Standing at the time of blood pressure measurement.">
				>
				["at1001"] = <
					text = <"Sitting">
					description = <"Sitting (for example on bed or chair) at the time of blood pressure measurement.">
				>
				["at1002"] = <
					text = <"Reclining">
					description = <"Reclining at the time of blood pressure measurement.">
				>
				["at1003"] = <
					text = <"Lying">
					description = <"Lying flat at the time of blood pressure measurement.">
				>
				["at1005"] = <
					text = <"Tilt">
					description = <"The craniocaudal tilt of the surface on which the person is lying at the time of measurement.">
				>
				["at1006"] = <
					text = <"Mean Arterial Pressure">
					description = <"The average arterial pressure that occurs over the entire course of the heart contraction and relaxation cycle.">
				>
				["at1007"] = <
					text = <"Pulse Pressure">
					description = <"The difference between the systolic and diastolic pressure.">
				>
				["at1008"] = <
					text = <"Small Adult">
					description = <"A cuff used for a small adult - bladder approx 10cm x 24cm.">
				>
				["at1009"] = <
					text = <"Paediatric/Child">
					description = <"A cuff that is appropriate for a child or adult with a thin arm - bladder approx 8cm x 21cm.">
				>
				["at1010"] = <
					text = <"Diastolic endpoint">
					description = <"Record which Korotkoff sound is used for determining diastolic pressure using auscultative method.">
				>
				["at1011"] = <
					text = <"Phase IV">
					description = <"The fourth Korotkoff sound is identified as an abrupt muffling of sounds.">
				>
				["at1012"] = <
					text = <"Phase V">
					description = <"The fifth Korotkoff sound is identified by absence of sounds as the cuff pressure drops below the diastolic blood pressure.">
				>
				["at1014"] = <
					text = <"Lying with tilt to left">
					description = <"Lying flat with some lateral tilt, usually angled towards the left side.   Commonly required in the last trimester of pregnancy to relieve aortocaval compression.">
				>
				["at1018"] = <
					text = <"Infant">
					description = <"A cuff used for infants - bladder approx 5cm x 15cm.">
				>
				["at1019"] = <
					text = <"Neonatal">
					description = <"A cuff used for a neonate, assuming cuff is the appropriate size for maturity and birthweight of the neonate.">
				>
				["at1020"] = <
					text = <"Right wrist">
					description = <"The right wrist of the subject.">
				>
				["at1021"] = <
					text = <"Left wrist">
					description = <"The left wrist of the subject.">
				>
				["at1025"] = <
					text = <"Device">
					description = <"Details about sphygmomanometer or other device used to measure the blood pressure.">
				>
				["at1026"] = <
					text = <"Right ankle">
					description = <"The right ankle of the subject.">
				>
				["at1030"] = <
					text = <"Exertion  ">
					description = <"Details about physical activity undertaken at the time of blood pressure.measurement.">
				>
				["at1031"] = <
					text = <"Left ankle">
					description = <"The left ankle of the subject.">
				>
				["at1032"] = <
					text = <"Finger">
					description = <"A finger of the subject.  Identification of the finger can be recorded in 'Specific Location' data element, if required.">
				>
				["at1033"] = <
					text = <"Location">
					description = <"Body location where blood pressure is measured.  Use 'Location of measurement' to select from common sites.  Use 'Specific location' to record more specific details or a site that is not in the common set or to refer to an external terminology.">
				>
				["at1034"] = <
					text = <"Specific location">
					description = <"Specific details about the body site where blood pressure is recorded.">
				>
				["at1035"] = <
					text = <"Method">
					description = <"Method of measurement of blood pressure.">
				>
				["at1036"] = <
					text = <"Auscultation">
					description = <"Method of measuring blood pressure externally, using a stethoscope and Korotkoff sounds.">
				>
				["at1037"] = <
					text = <"Palpation">
					description = <"Method of measuring blood pressure externally, using palpation (usually of the brachial or radial arteries).">
				>
				["at1038"] = <
					text = <"Mean Arterial Pressure Formula">
					description = <"Formula used to calculate the MAP (if recorded in data).">
				>
				["at1039"] = <
					text = <"Machine">
					description = <"Method of measuring blood pressure externally, using a blood pressure machine.">
				>
				["at1040"] = <
					text = <"Invasive">
					description = <"Method of measuring blood pressure internally ie involving penetration of the skin and measuring inside blood vessels.">
				>
				["at1042"] = <
					text = <"24 hour average ">
					description = <"Estimate of the average blood pressure over a 24 hour period.">
				>
				["at1043"] = <
					text = <"Sleep status">
					description = <"Sleep status - supports interpretation of 24 hour ambulatory blood pressure records.">
				>
				["at1044"] = <
					text = <"Alert & awake">
					description = <"Subject is fully conscious.">
				>
				["at1045"] = <
					text = <"Sleeping">
					description = <"Subject is in the natural state of bodily rest.">
				>
				["at1051"] = <
					text = <"Toe">
					description = <"A toe of the subject.   Identification of the toe can be recorded in 'Specific Location' data element, if required.">
				>
				["at1052"] = <
					text = <"Confounding factors">
					description = <"Comment on and record other incidental factors that may be contributing to the blood pressure measurement.  For example, level of anxiety or 'white coat syndrome'; pain or fever; changes in atmospheric pressure etc.">
				>
				["at1053"] = <
					text = <"Intra-arterial">
					description = <"Invasive measurement via transducer access line within an artery. Location of the transducer can be recorded in 'Specific Location' data element, if required.">
				>
			>
		>
		["pt-br"] = <
			items = <
				["at0000"] = <
					text = <"Pressão Arterial">
					description = <"A medida local da pressão sanguínea arterial, a qual é uma substituta da pressão arterial na circulação arterial sistêmica. Mais comumente o uso do termo pressão arterial se refere à medida da pressão da artéria braquial no antebraço.">
				>
				["at0001"] = <
					text = <"história">
					description = <"nodo Estrutural história">
				>
				["at0003"] = <
					text = <"*blood pressure(en)">
					description = <"*@ internal @(en)">
				>
				["at0004"] = <
					text = <"Sistólica">
					description = <"Pressão arterial sistêmica máxima - medida na fase sistólica ou de contração do ciclo cardíaco.">
				>
				["at0005"] = <
					text = <"Diastólica">
					description = <"Pressão arterial sistêmica mínima - medida na fase diastólica ou de  dilatação do ciclo cardíaco.">
				>
				["at0006"] = <
					text = <"qualquer evento">
					description = <"evento Default, padrão predeterminado">
				>
				["at0007"] = <
					text = <"*state structure(en)">
					description = <"*@ internal @(en)">
				>
				["at0008"] = <
					text = <"Posição">
					description = <"A posição do sujeito na hora da medida.">
				>
				["at0011"] = <
					text = <"estrutura de lista">
					description = <"estrutura de lista">
				>
				["at0013"] = <
					text = <"Tamanho da braçadeira">
					description = <"Tamanho da braçadeira  usada para medir a pressão arterial.">
					comment = <"Sem  comentários">
				>
				["at0014"] = <
					text = <"Local de Medida">
					description = <"Locais do corpo mais comuns onde a pressão arterial é medida.">
				>
				["at0015"] = <
					text = <"Coxa de adulto">
					description = <"Uma braçadeira  usada na coxa de um aduto - manguito de aproximadamente 20cm x 42cm.">
				>
				["at0016"] = <
					text = <"Adulto Grande">
					description = <"Uma braçadeira para adultos com braço largo - manguito de aproximadamente 16cm x 38cm.">
				>
				["at0017"] = <
					text = <"Adulto">
					description = <"Uma braçadeira padrão para adultos - manguito de aproximadamente 13cm x 30cm.">
				>
				["at0025"] = <
					text = <"Braço direito">
					description = <"O braço direito da pessoa.">
				>
				["at0026"] = <
					text = <"Braço esquerdo">
					description = <"O braço esquerdo da pessoa.">
				>
				["at0027"] = <
					text = <"Coxa direita">
					description = <"A coxa direita da pessoa.">
				>
				["at0028"] = <
					text = <"Coxa esquerda">
					description = <"A coxa esquerda da pessoa.">
				>
				["at0033"] = <
					text = <"Comentários">
					description = <"Comentários sobre a medida da pressão arterial.">
				>
				["at1000"] = <
					text = <"Em pé">
					description = <"Em pé na hora da medida da pressão arterial.">
				>
				["at1001"] = <
					text = <"Sentado">
					description = <"Sentado (por exemplo na cama ou em uma cadeira) na hora da medida da pressão arterial.">
				>
				["at1002"] = <
					text = <"Reclinado">
					description = <"Reclinado na hora da medida da pressão arterial.">
				>
				["at1003"] = <
					text = <"Deitado">
					description = <"Deitado sem reclinação hora da medida da pressão arterial.">
				>
				["at1005"] = <
					text = <"Nível de inclinação">
					description = <"O nível de  inclinação crânio-caudal da superfície sobre a qual a pessoa está deitada no momento da medição.">
				>
				["at1006"] = <
					text = <"Pressão arterial Média">
					description = <"A pressão arterial média que ocorrre ao longo de todo o  ciclo de contração e dilatação do coração.">
				>
				["at1007"] = <
					text = <"Pressão de Pulso">
					description = <"A diferença entre a pressão sistólica e diastólica.">
				>
				["at1008"] = <
					text = <"Adulto pequeno">
					description = <"Uma braçadeira usada para adultos pequenos -manguito de aproximadamente 10cm x 24cm.">
				>
				["at1009"] = <
					text = <"Criança/Pediátrico">
					description = <"Manguito apropriao para uma criança ou um adulto com um braço fino - bolsa de aproximadamente 8cm x 21cm.">
				>
				["at1010"] = <
					text = <"Final da diástole">
					description = <"Registro  do som Korotkoff usado para determinar a pressão arterial diastólica, usando o método auscultativo.">
				>
				["at1011"] = <
					text = <"Fhase IV.">
					description = <"O quarto som de  Korotkoff é  identificado como um súbito abafamento dos sons.">
				>
				["at1012"] = <
					text = <"Phase V">
					description = <"O quinto som de Korotkoff é identificado pela ausência de sons,  pois a pressão da braçadeira cai abaixo da pressão diastólica do sangue.">
				>
				["at1014"] = <
					text = <"Deitado com inclinação para esquerda">
					description = <"Deitado sem reclinação com alguma inclinação lateral, usualmente com angulação para o lado esquerdo. Comumente requerido no último trimestre da gravidez para aliviar a compressão aortocaval.">
				>
				["at1018"] = <
					text = <"Criança pequena ">
					description = <"Uma braçadeira utilizada em crianças  pequenas - manguito de aproximadamente 5cm x 15cm.">
				>
				["at1019"] = <
					text = <"Neonatal">
					description = <"Uma braçadeira usada para um recém-nascido, supondo que o tamanho é apropriado para a maturidade e o peso ao nascer do neonato.">
				>
				["at1020"] = <
					text = <"Pulso direito">
					description = <"O pulso direito da pessoa.">
				>
				["at1021"] = <
					text = <"Pulso esquerdo">
					description = <"O pulso esquerdo da pessoa.">
				>
				["at1025"] = <
					text = <"Aparelho">
					description = <"Detalhes sobre o esfigmomanômetro ou outro aparelho utilizado para medir a pressão sanguínea.">
				>
				["at1026"] = <
					text = <"Tornozelo direito">
					description = <"O tornozelo direito da pessoa.">
				>
				["at1030"] = <
					text = <"Esforço físico">
					description = <"Detalhes sobre atividade física realizada na hora da medida da pressão arterial.">
				>
				["at1031"] = <
					text = <"Tornozelo esquerdo">
					description = <"O tornozelo esquerdo da pessoa.">
				>
				["at1032"] = <
					text = <"Dedo da mão">
					description = <"O dedo da mão da pessoa. A identificação do dedo pode ser registrada no elemento de dado 'Local Específico', se requerido.">
				>
				["at1033"] = <
					text = <"Local">
					description = <"Local do corpo onde  foi medida a pressão arterial.  Use  'Local da Medida' para selecionar entre os locais mais comuns.  Use 'Local Específico' para registrar detalhes mais específicos ou um local que não está representado no conjunto comum ou para se referenciar a uma terminologia externa.">
				>
				["at1034"] = <
					text = <"Local Específico">
					description = <"Detalhes específicos sobre o local do corpo onde a pressão arterial é registrada.">
				>
				["at1035"] = <
					text = <"Método">
					description = <"Método de medida da pressão arterial.">
				>
				["at1036"] = <
					text = <"Ausculta">
					description = <"Método de medir a pressão externamente, usando o estetoscópiso e os sons Korotkoff.">
				>
				["at1037"] = <
					text = <"Palpação">
					description = <"Método de medir a pressão arterial externamente, usando a palpação (geralmente artérias braquiais ou radiais).">
				>
				["at1038"] = <
					text = <"Fórmula de Pressão Arterial Média">
					description = <"Fórmula usada para calcular a pressão arterial média (PAM), se registrada em dados.">
				>
				["at1039"] = <
					text = <"Aparelho de pressão">
					description = <"Método de medir a pressão arterial externamente, usando o aparelho de pressão arterial.">
				>
				["at1040"] = <
					text = <"Invasivo">
					description = <"Método de medir a pressão arterial internamente, isto é, envolvendo a penetração da pele e a medida interior dos vasos sanguíneos.">
				>
				["at1042"] = <
					text = <"Media de 24 horas">
					description = <"Estimativa da pressão arterial média em um período de 24 horas.">
				>
				["at1043"] = <
					text = <"Status do sono">
					description = <"Status do sono - apóia a interpretação do registro do mapa de pressão arterial (ambulatorial de 24 horas).">
				>
				["at1044"] = <
					text = <"Alerta e acordado">
					description = <"Sujeito está totalmente consciente.">
				>
				["at1045"] = <
					text = <"Dormindo">
					description = <"Sujeito está no estado natural de descanso corporal.">
				>
				["at1051"] = <
					text = <"Dedo do pé">
					description = <"O dedo do pé da pessoa. A identificação do dedo do pé pode ser registrada  no elemento de dado 'Local Específico', se requerido.">
				>
				["at1052"] = <
					text = <"Fatores confundidores">
					description = <"Comentários sobre e registros de outros fatores incidentais que possam estar contribuindo na medida da pressão sanguínea. Por exemplo, nível de ansiedade ou \"síndrome do jaleco branco\"; dor ou febre; mudanças na pressão atmosférica, etc.">
				>
				["at1053"] = <
					text = <"Intra-arterial">
					description = <"Medida invasiva da pressão em uma artéria acessada  por um cateter . Local do transductor pode ser registrado no elemento de dado  'Local Específico', se requerido.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0000"] = <[SNOMED-CT(2003)::163020007]>
				["at0004"] = <[SNOMED-CT(2003)::163030003]>
				["at0005"] = <[SNOMED-CT(2003)::163031004]>
				["at0013"] = <[SNOMED-CT(2003)::246153002]>
			>
		>
	>
